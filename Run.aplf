 {toc}←{stop}Run args;y;i;p;name;src;dir;t;tests;ts;urls;us;u;_;chat;code;video;v;title;m;desc;slash;dash;url;durs;ds;d
 dir←⊃⊆args
 toc←0⍴⊂''
 urls←0 ⎕JSON⊃⎕NGET dir,'src/urls.json'
 durs←11 10⍴⊃⎕NGET(dir,'src/durs.txt')1
 1 ⎕MKDIR dir,'docs/psets'
 (
     '<!DOCTYPE html>'
     '<title>APL Quest</title>'
     '<meta http-equiv=refresh content="0; url=https://apl.quest/">'
 )⊂⍛⎕NPUT(dir,'docs/psets/help.html')1
 :For y us ds :InEach (⍕¨2012+⍳11)urls(↓durs)
     1 ⎕MKDIR dir,'docs/',y
     src←⊃⎕NGET(dir,'src/',y,'.md')1
     src⊂⍨←'<div id="P'∘(⊃⍷)¨src
     toc,←⊂'    - ',y,':'
     tests←0 ⎕JSON⊃⎕NGET dir,'src/',y,'.json'
     ts←1⌽1↓⊃⌽tests ⎕VGET 9
     (
         '<!DOCTYPE html>'
         '<title>',y,': APL Quest</title>'
         '<script>window.location.replace(document.location.href.replace(/\.html(\?goto=P)?/,document.location.search?"/":"/1").replace(/psets\/|_.+/g,""))</script>'
     )⊂⍛⎕NPUT(dir,'docs/psets/',y,'.html')1
     :For i p t u d :InEach (⍕¨⍳10)src ts us ds
         (_ chat code video)←u
         p←'^(# )(\d+: )'⎕R('\1<span class=s>',y,'-</span>\2')⊢p
         name←2↓⊃p/⍨m←'# '∘(⊃⍷)¨p
         desc←⊃'rite a[^.]+\.'⎕S'W&'⊢p
         title←'</?span.*?>'⎕R''⊢name
         slash←y,'/',i
         dash←y,'-',i
         :If 2=⎕NC'stop'
         :AndIf stop⊂⍛∊dash slash(y i)(⍎¨y i)
             (1+⊃⎕LC)⎕STOP⊃⎕SI
         :EndIf
         p←(
             '---'
             'title: "APL Quest ',title,'"'
             'description: "',desc,'"'
             'image: "https://apl.quest/img/',dash,'.png"'
             'url: "https://apl.quest/',slash,'"'
             '---'
         ),p

         p/⍨←~'<div id="P'∘(⊃⍷)¨p
         p/⍨←~⌽<\⌽(p~¨' ')∊⊂'</div>'
         p/⍨←~(∧\∨∘⌽∧\∘⌽)''∘≡¨p
         v←⊃'v=(.+)'⎕S'\1'⊢video
         p,←(
             '<div class="pdiv">'
             '  <code onclick="p_Input.focus()">your_function ← </code><input id="p_Input" autocomplete="off" spellcheck="false" oninput="this.parentElement.querySelector`button`.disabled=false;localStorage.setItem(window.location.pathname,this.value)" onkeypress="subm(event)">'
             '  <button onclick="alert$.next`Testing…`;submitSolution`p`" class="md-button md-button--primary">&#x2714; Test</button>'
             '</div>'
             '<p id="p_Output"></p>'
             '## Solutions'
             '<div onclick="play(this)" title="Video on YouTube" class="yt">'
             '<img alt="Video Thumbnail" src="../../img/',dash,'.png">'
             '<time>',d,'</time>'
             '<img alt="YouTube" src="../../img/yt-big.png">'
             '</div>'
             '<a href="',chat,'" target="_blank" class="md-button md-button--primary">Chat transcript</a>'
             '<a href="',code,'" target="_blank" class="md-button md-button--primary right">Code on GitHub</a>'
             ''
             '<script>'
             '    testCases=',1 ⎕JSON t
             '    p_Input.value=localStorage.getItem(window.location.pathname)'
             '    play=e=>e.outerHTML=`<iframe src="https://www.youtube.com/embed/',('&'⎕R'?'⊢v),'&autoplay=1" title="',name,' (APL Quest ',dash,')" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture; web-share" referrerpolicy="strict-origin-when-cross-origin" allowfullscreen></iframe>`'
             '</script>'
         )
         p⊂⍛⎕NPUT(dir,'docs/',slash,'.md')1
         toc,←⊂'      - "',(''''⎕R'\\u0027'⊢name),'": "',slash,'.md"'
     :EndFor
 :EndFor
